apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = "io.ona.rdt-capture"
version = '0.1.1'

android {
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 33
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    task sourceJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier "sources"
    }

    artifacts {
        archives sourceJar
    }

    def githubProperties = new Properties()
    if (rootProject.file("github.properties").exists()) {
        githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
    }

    publishing {
        publications {
            OpenCVLib(MavenPublication) {
                artifact("$buildDir/outputs/aar/openCVLibrary341-contrib-release.aar")
                artifact(sourceJar)
                groupId this.group
                artifactId 'opencvlibrary341-contrib'
                version this.version

                //The publication doesn't know about our dependencies, so we have to manually add them to the pom
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                /** Configure path of your package repository on Github
                 ** Replace GITHUB_USERID with your/organisation Github userID
                 ** and REPOSITORY with the repository name on GitHub
                 */
                url = uri("https://maven.pkg.github.com/Digital-Square-Tanzania/rdt-capture")
                credentials {
                    /** Create github.properties in root project folder file with
                     ** gpr.usr=GITHUB_USER_ID & gpr.key=PERSONAL_ACCESS_TOKEN
                     ** Set env variable GPR_debugdebugUSER & GPR_API_KEY if not adding a properties file**/

                    username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}
