apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = "io.ona.rdt-capture"
version = '0.1.1'

android {
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    task sourceJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier "sources"
    }

    artifacts {
        archives sourceJar
    }

    publishing {
        publications {
            OpenCVLib(MavenPublication) {
                artifact("$buildDir/outputs/aar/openCVLibrary341-contrib-release.aar")
                artifact(sourceJar)
                groupId this.group
                artifactId 'openCVLibrary341-contrib'
                version this.version

                //The publication doesn't know about our dependencies, so we have to manually add them to the pom
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}
